// SPDX-License-Identifier: MIT
pragma solidity 0.8.28;

import {DeepstateController} from "./DeepstateController.sol";
import {IDeepstateV1} from "./interfaces/IDeepstateV1.sol";

/// @title Deepstate V1 Controller
/// @notice Governance-owned capability boundary around the single-owner Deepstate router.
contract DeepstateV1Controller is DeepstateController {
    uint256 public constant HOOK_MANAGER_ROLE = 1 << 0;

    IDeepstateV1 public immutable deepstate;

    event DeepstateFeeConfigured(address indexed recipient, uint16 bps);
    event DeepstateOwnershipTransferred(address indexed newOwner);

    error InvalidDeepstate();

    constructor(address owner_, address deepstate_) DeepstateController(owner_) {
        if (deepstate_ == address(0) || deepstate_.code.length == 0) revert InvalidDeepstate();

        deepstate = IDeepstateV1(deepstate_);
    }

    /// @notice Configure one pool hook as governance or the delegated hook manager.
    function setPoolHookConfig(address token0, address token1, address hook, bool token0Active, bool token1Active)
        external
        onlyOwnerOrRoles(HOOK_MANAGER_ROLE)
    {
        deepstate.setPoolHookConfig(token0, token1, hook, token0Active, token1Active);
    }

    /// @notice Configure protocol fees. The hook manager has no access to this capability.
    function setDeepstateFeeConfig(address recipient, uint16 bps) external onlyOwner {
        deepstate.setFeeConfig(recipient, bps);
        emit DeepstateFeeConfigured(recipient, bps);
    }

    /// @notice Return router ownership to governance or another governance-approved owner.
    function transferDeepstateOwnership(address newOwner) external onlyOwner {
        deepstate.transferOwnership(newOwner);
        emit DeepstateOwnershipTransferred(newOwner);
    }
}
